plugins {
        id 'org.openapi.generator' version '6.4.0'
	id 'java'
	id 'war'
	id 'org.springframework.boot' version '3.1.2'
	id 'io.spring.dependency-management' version '1.1.2'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'

java {
	sourceCompatibility = '17'
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	providedRuntime 'org.springframework.boot:spring-boot-starter-tomcat'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'

        // required for code generation
        implementation('org.openapitools:jackson-databind-nullable:+')
        implementation('org.springframework.boot:spring-boot-starter-validation')

        // For Client
        implementation("org.springframework.boot:spring-boot-starter-webflux")
}

tasks.named('test') {
	useJUnitPlatform()
}

ext {
  serviceSpec="${projectDir}/src/main/resources/openapi.yaml"
}

sourceSets {
  main {
    java {
      srcDir "${project.buildDir}/openapi-generated-server-source/src/main/java"
      srcDir "${project.buildDir}/openapi-generated-client-source/src/main/java"
    }
  }
}

task generateServerStubs(type: org.openapitools.generator.gradle.plugin.tasks.GenerateTask) {
  generatorName = 'spring'
  inputSpec = serviceSpec
  outputDir = "${project.buildDir}/openapi-generated-server-source"
  apiPackage = 'com.example.openapidemo.controller'
  modelPackage = 'com.example.openapidemo.generated.dto'
  configOptions = [
      'containerDefaultToNull' : 'true',
      'dateLibrary'          : 'custom',
      'documentationProvider': 'none',
      'interfaceOnly'        : 'true',
      'performBeanValidation': 'true',
      'skipDefaultInterface' : 'true',
      'useBeanValidation'    : 'true',
      'useSpringBoot3'       : 'true',
      'useTags'              : 'true',
  ]
  typeMappings = [
      'DateTime': 'Instant'
  ]
  importMappings = [
      'Instant': 'java.time.Instant'
  ]
}

compileJava.dependsOn(generateServerStubs)

task generateClientCode(type: org.openapitools.generator.gradle.plugin.tasks.GenerateTask) {
  generatorName = "java"
  inputSpec = serviceSpec
  outputDir = "${buildDir}/openapi-generated-client-source"
  groupId = "${group}"
  id = 'signal-service-client-sdk'
  version = "${version}"
  apiPackage = 'com.example.openapi.client.api'
  modelPackage = 'com.example.openapi.client.model'
  enablePostProcessFile = true
  library = 'webclient'
  skipOverwrite = false
  configOptions = [
      'containerDefaultToNull' : 'true',
      'dateLibrary'            : 'custom',
      'useJakartaEe'           : 'true',
      'useSpringBoot3'         : 'true',
  ]
  typeMappings = [
      'DateTime': 'Instant'
  ]
  importMappings = [
      'Instant': 'java.time.Instant'
  ]
}
compileJava.dependsOn(generateClientCode)

